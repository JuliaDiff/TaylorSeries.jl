# User guide

---

    {meta}
    CurrentModule = TaylorSeries
    DocTestSetup = quote
        using TaylorSeries
        affine(a) = a + taylor1_variable(typeof(a),5)
        t = taylor1_variable(5)
        tI = im * t
        x, y = set_variables("x y", order=10);
        exy = exp(x+y)
        f(x,y) = x^3 + 2x^2 * y - 7x + 2
        g(x,y) = y - x^4
    end

`TaylorSeries.jl` can be thought of as a polynomial algebraic manipulator in one or more
variables; these two cases are treated separately.  Three new types are defined,
`Taylor1`, `HomogeneousPolynomial` and `TaylorN`, which correspond to
expansions in one independent variable, homogeneous polynomials of various variables, and the polynomial
series in many independent variables, respectively. These types are subtypes
of `Number` and are defined parametrically.

The package is loaded as usual:

```julia
julia> using TaylorSeries

```

## One variable

Taylor expansions in one variable are represented by the `Taylor1` type, which
consists of a vector of coefficients (field `coeffs`) and the maximum
order considered for the expansion (field `order`). The
coefficients are arranged in ascending order with respect to the power of the
independent variable, so that
`coeffs[1]` is the constant term, `coeffs[2]` gives the first order term,
etc. This is a dense representation of the polynomial.
The order of the polynomial can be
omitted in the constructor, which is then fixed from the length of the
vector of coefficients; otherwise, the maximum
of the length of the vector of coefficients and the given integer is taken.

```julia
julia> Taylor1([1, 2, 3]) # Polynomial of order 2 with coefficients 1, 2, 3
 1 + 2 t + 3 tÂ² + ð’ª(tÂ³)

julia> Taylor1([0.0, 1im]) # Also works with complex numbers
 ( 1.0 im ) t + ð’ª(tÂ²)

julia> affine(a) = a + taylor1_variable(typeof(a),5)  ## a + t of order 5
affine (generic function with 1 method)

julia> t = affine(0.0) # Independent variable `t`
 1.0 t + ð’ª(tâ¶)

```

Note that the information about the maximum order considered is displayed
using a big-O notation.

The definition of `affine(a)` uses the function `taylor1_variable`, which is a
shortcut to define the independent variable of a Taylor expansion,
with a given type and given order. As we show below, this is one of the
easiest ways to work with the package.

The usual arithmetic operators (`+`, `-`, `*`, `/`, `^`, `==`) have been
extended to work with the `Taylor1` type, including promotions that involve
`Number`s. The operations return a valid Taylor expansion with the same
maximum order; compare the last example below, where this is not possible:

```julia
julia> t*(3t+2.5)
 2.5 t + 3.0 tÂ² + ð’ª(tâ¶)

julia> 1/(1-t)
 1.0 + 1.0 t + 1.0 tÂ² + 1.0 tÂ³ + 1.0 tâ´ + 1.0 tâµ + ð’ª(tâ¶)

julia> t*(t^2-4)/(t+2)
 - 2.0 t + 1.0 tÂ² + ð’ª(tâ¶)

julia> tI = im*t
 ( 1.0 im ) t + ð’ª(tâ¶)

julia> t^6  # order is 5
 0.0 + ð’ª(tâ¶)

julia> (1-t)^3.2
 1.0 - 3.2 t + 3.5200000000000005 tÂ² - 1.4080000000000004 tÂ³ + 0.07040000000000009 tâ´ + 0.011264000000000012 tâµ + ð’ª(tâ¶)

julia> (1+t)^t
 1.0 + 1.0 tÂ² - 0.5 tÂ³ + 0.8333333333333333 tâ´ - 0.75 tâµ + ð’ª(tâ¶)

```

If no valid Taylor expansion can be computed, an error is thrown.

```julia
julia> 1/t
ERROR: ArgumentError: Division does not define a Taylor1 polynomial
or its first non-zero coefficient is Inf/NaN.
Order k=0 => coeff[1]=Inf.
 in divfactorization at /Users/benet/.julia/v0.4/TaylorSeries/src/Taylor1.jl:239
 in / at /Users/benet/.julia/v0.4/TaylorSeries/src/Taylor1.jl:215
 in / at ./promotion.jl:170

julia> t^3.2
ERROR: ArgumentError: The 0th order Taylor1 coefficient must be non-zero
to raise the Taylor1 polynomial to a non-integer exponent.
 in ^ at /Users/benet/.julia/v0.4/TaylorSeries/src/Taylor1.jl:355

```

Several elementary functions have been implemented; these compute their
coefficients recursively. So far, these functions are `exp`, `log`, `sqrt`, `sin`, `cos`
and `tan`;
more will be added in the future. Note that this way of obtaining the
Taylor coefficients is not the *laziest* way, in particular for many independent
variables. Yet, it is quite efficient, especially for the integration of
ordinary differential equations, which is among the applications we have in mind.

```julia
julia> exp(t)
 1.0 + 1.0 t + 0.5 tÂ² + 0.16666666666666666 tÂ³ + 0.041666666666666664 tâ´ + 0.008333333333333333 tâµ + ð’ª(tâ¶)

julia> log(1-t)
 - 1.0 t - 0.5 tÂ² - 0.3333333333333333 tÂ³ - 0.25 tâ´ - 0.2 tâµ + ð’ª(tâ¶)

julia> sqrt(t)
ERROR: ArgumentError: First non-vanishing Taylor1 coefficient must correspond
to an **even power** in order to expand `sqrt` around 0.
 in sqrt at /Users/benet/.julia/v0.4/TaylorSeries/src/Taylor1.jl:427

julia> sqrt(1 + t)
 1.0 + 0.5 t - 0.125 tÂ² + 0.0625 tÂ³ - 0.0390625 tâ´ + 0.02734375 tâµ + ð’ª(tâ¶)

julia> imag(exp(tI)')
 - 1.0 t + 0.16666666666666666 tÂ³ - 0.008333333333333333 tâµ + ð’ª(tâ¶)

julia> real(exp(Taylor1([0.0,1im],17))) - cos(Taylor1([0.0,1.0],17)) == 0.0
true

julia> convert(Taylor1{Rational{Int64}}, exp(t))
 1//1 + 1//1 t + 1//2 tÂ² + 1//6 tÂ³ + 1//24 tâ´ + 1//120 tâµ + ð’ª(tâ¶)

```

Differentiating and integrating is straightforward for polynomial expansions in
one variable. The last coefficient of a derivative is set to zero to keep the
same order as the original polynomial; for the integral, an
integration constant may be set to a different value (the default is zero). The
order of the resulting polynomial is not changed. The $n$-th ($n \ge 0$)
derivative is obtained using `deriv(a,n)`, where `a` is a Taylor series;
the default is $n=1$.

```julia
julia> diffTaylor(exp(t))
 1.0 + 1.0 t + 0.5 tÂ² + 0.16666666666666666 tÂ³ + 0.041666666666666664 tâ´ + ð’ª(tâ¶)

julia> integTaylor(exp(t))
 1.0 t + 0.5 tÂ² + 0.16666666666666666 tÂ³ + 0.041666666666666664 tâ´ + 0.008333333333333333 tâµ + ð’ª(tâ¶)

julia> integTaylor( exp(t), 1.0)
 1.0 + 1.0 t + 0.5 tÂ² + 0.16666666666666666 tÂ³ + 0.041666666666666664 tâ´ + 0.008333333333333333 tâµ + ð’ª(tâ¶)

julia> integTaylor( diffTaylor( exp(-t)), 1.0 ) == exp(-t)
true

julia> deriv( exp(affine(1.0))) == exp(1.0)
true

julia> deriv( exp(affine(1.0)), 5) == exp(1.0) # Fifth derivative of `exp(1+t)`
true

```

To evaluate a Taylor series at a point, Horner's rule is used via the function
`evaluate(a::Taylor, dt::Number)`. Here, $dt$ is the increment from
the point $t_0$ where the Taylor expansion is calculated, i.e., the series
is evaluated at $t = t_0 + dt$. Omitting $dt$ corresponds to $dt = 0$.

```julia
julia> evaluate(exp(affine(1.0))) - e # exp(t) around t0=1 (order 5), evaluated there (dt=0)

0.0

julia> evaluate(exp(t), 1) - e # exp(t) around t0=0 (order 5), evaluated at t=1
-0.0016151617923783057

julia> evaluate( exp( taylor1_variable(17) ), 1) - e # exp(t) around t0=0 (order 17),
0.0

julia> tBig = Taylor1([zero(BigFloat),one(BigFloat)],50) # With BigFloats
 1.000000000000000000000000000000000000000000000000000000000000000000000000000000 t + ð’ª(tâµÂ¹)

julia> eBig = evaluate( exp(tBig), one(BigFloat) )
2.718281828459045235360287471352662497757247093699959574966967627723419298053556

julia> e - eBig
6.573322999985292556154129119543257102601105719980995128942636339920549561322098e-67

```


## Many variables

A polynomial in $N>1$ variables can be represented in (at least) two ways:
As a vector whose coefficients are homogeneous polynomials of fixed degree, or
as a vector whose coefficients are polynomials in $N-1$ variables. We have opted
to implement the first option, which seems to show better performance. An elegant
(lazy) implementation of the second representation was discussed on the
[julia-users](https://groups.google.com/forum/#!msg/julia-users/AkK_UdST3Ig/sNrtyRJHK0AJ) list.

`TaylorN` is thus constructed as a vector of parameterized homogeneous polynomials
defined by the type `HomogeneousPolynomial`, which in turn is a vector of
coefficients of given order (degree). This implementation imposes that the user
has to specify the (maximum) order and the number of independent
variables, which is done using the `set_variables(names)` function.
`names` is a string consisting of the desired *output* names of the variables,
separated by spaces. A vector of the resulting Taylor variables is returned:

```julia
julia> x, y = set_variables("x y")
2-element Array{TaylorSeries.TaylorN{Float64},1}:
  1.0 x + ð’ª(â€–xâ€–â·)
  1.0 y + ð’ª(â€–xâ€–â·)

julia> x
 1.0 x + ð’ª(â€–xâ€–â·)

julia> typeof(x)
TaylorSeries.TaylorN{Float64}

julia> x.order
6

julia> x.coeffs
7-element Array{TaylorSeries.HomogeneousPolynomial{Float64},1}:
    0.0
  1.0 x
    0.0
    0.0
    0.0
    0.0
    0.0

```

As shown, the resulting objects are of `TaylorN{Float64}` type.
There is an optional `order` keyword argument for `set_variables`:

```julia
julia> set_variables("x y", order=10)
2-element Array{TaylorSeries.TaylorN{Float64},1}:
  1.0 x + ð’ª(â€–xâ€–Â¹Â¹)
  1.0 y + ð’ª(â€–xâ€–Â¹Â¹)

```

Numbered variables are also available by specifying a single
variable name and the optional keyword argument `numvars`:

```julia
julia> set_variables("Î±", numvars=3)
3-element Array{TaylorSeries.TaylorN{Float64},1}:
  1.0 Î±â‚ + ð’ª(â€–xâ€–â·)
  1.0 Î±â‚‚ + ð’ª(â€–xâ€–â·)
  1.0 Î±â‚ƒ + ð’ª(â€–xâ€–â·)

```

The function `show_params_TaylorN()` displays the current values of the
parameters, in an info block.

    julia> show_params_TaylorN()

    INFO: Parameters for `TaylorN` and `HomogeneousPolynomial`:
    Maximum order       = 6
    Number of variables = 3
    Variable names      = UTF8String["Î±â‚","Î±â‚‚","Î±â‚ƒ"]


Internally, changing these parameters defines dictionaries that
translate the position of the coefficients of a `HomogeneousPolynomial`
into the corresponding
multi-variable monomials. Fixing these values from the start is imperative.

The easiest way to construct a `TaylorN` object is by defining symbols for
the independent variables, as above. Again, the Taylor expansions are implemented
around 0 for all variables; if the expansion
is needed around a different value, the trick is a simple translation of
the corresponding
independent variable $x \to x+a$.

Other ways of constructing `TaylorN` polynomials involve using `HomogeneousPolynomial`
objects directly, which is uncomfortable:

```julia
julia> set_variables("x", numvars=2);

julia> HomogeneousPolynomial([1,-1])
 1 xâ‚ - 1 xâ‚‚

julia> TaylorN([HomogeneousPolynomial([1,0]), HomogeneousPolynomial([1,2,3])],4)
 1 xâ‚ + 1 xâ‚Â² + 2 xâ‚ xâ‚‚ + 3 xâ‚‚Â² + ð’ª(â€–xâ€–âµ)

```

As before, the usual arithmetic operators (`+`, `-`, `*`, `/`, `^`, `==`)
have been extended to work with `TaylorN` objects, including the appropriate
promotions to deal with numbers. (Some of the arithmetic operations have
also been extended for
`HomogeneousPolynomial`, whenever the result is a `HomogeneousPolynomial`;
division, for instance, is not extended.) Also, the elementary functions have been
implemented, again by computing their coefficients recursively:

```julia
julia> x, y = set_variables("x y", order=10);

julia> exy = exp(x+y)
 1.0 + 1.0 x + 1.0 y + 0.5 xÂ² + 1.0 x y + 0.5 yÂ² + 0.16666666666666666 xÂ³ + 0.5 xÂ² y + 0.5 x yÂ² + 0.16666666666666666 yÂ³ + 0.041666666666666664 xâ´ + 0.16666666666666666 xÂ³ y + 0.25 xÂ² yÂ² + 0.16666666666666666 x yÂ³ + 0.041666666666666664 yâ´ + 0.008333333333333333 xâµ + 0.041666666666666664 xâ´ y + 0.08333333333333333 xÂ³ yÂ² + 0.08333333333333333 xÂ² yÂ³ + 0.041666666666666664 x yâ´ + 0.008333333333333333 yâµ + 0.0013888888888888887 xâ¶ + 0.008333333333333331 xâµ y + 0.020833333333333332 xâ´ yÂ² + 0.027777777777777776 xÂ³ yÂ³ + 0.020833333333333332 xÂ² yâ´ + 0.008333333333333331 x yâµ + 0.0013888888888888887 yâ¶ + 0.00019841269841269839 xâ· + 0.0013888888888888885 xâ¶ y + 0.004166666666666666 xâµ yÂ² + 0.006944444444444443 xâ´ yÂ³ + 0.006944444444444443 xÂ³ yâ´ + 0.004166666666666666 xÂ² yâµ + 0.0013888888888888885 x yâ¶ + 0.00019841269841269839 yâ· + 2.4801587301587298e-5 xâ¸ + 0.00019841269841269836 xâ· y + 0.0006944444444444443 xâ¶ yÂ² + 0.0013888888888888887 xâµ yÂ³ + 0.0017361111111111108 xâ´ yâ´ + 0.0013888888888888887 xÂ³ yâµ + 0.0006944444444444443 xÂ² yâ¶ + 0.00019841269841269836 x yâ· + 2.4801587301587298e-5 yâ¸ + 2.7557319223985884e-6 xâ¹ + 2.4801587301587295e-5 xâ¸ y + 9.920634920634918e-5 xâ· yÂ² + 0.0002314814814814814 xâ¶ yÂ³ + 0.0003472222222222221 xâµ yâ´ + 0.0003472222222222221 xâ´ yâµ + 0.0002314814814814814 xÂ³ yâ¶ + 9.920634920634918e-5 xÂ² yâ· + 2.4801587301587295e-5 x yâ¸ + 2.7557319223985884e-6 yâ¹ + 2.7557319223985883e-7 xÂ¹â° + 2.7557319223985884e-6 xâ¹ y + 1.2400793650793647e-5 xâ¸ yÂ² + 3.306878306878306e-5 xâ· yÂ³ + 5.787037037037036e-5 xâ¶ yâ´ + 6.944444444444443e-5 xâµ yâµ + 5.787037037037036e-5 xâ´ yâ¶ + 3.306878306878306e-5 xÂ³ yâ· + 1.2400793650793647e-5 xÂ² yâ¸ + 2.7557319223985884e-6 x yâ¹ + 2.7557319223985883e-7 yÂ¹â° + ð’ª(â€–xâ€–Â¹Â¹)

```

The function `get_coeff(a,v)`
gives the coefficient of `x` that corresponds to the monomial
specified by the vector of powers `v`:

```julia
julia> get_coeff(exy, [3,5]) == 1/720
false

julia> rationalize(get_coeff(exy, [3,5]))
1//720

```

Partial differentiation is also implemented for `TaylorN` objects,
through `diffTaylor`; integration is yet to be implemented.

```julia
julia> f(x,y) = x^3 + 2x^2 * y - 7x + 2
f (generic function with 1 method)

julia> g(x,y) = y - x^4
g (generic function with 1 method)

julia> diffTaylor( f(x,y), 1 )   # partial derivative with respect to 1st variable
 - 7.0 + 3.0 xÂ² + 4.0 x y + ð’ª(â€–xâ€–Â¹Â¹)

julia> diffTaylor( g(x,y), 2 )
 1.0 + ð’ª(â€–xâ€–Â¹Â¹)

julia> diffTaylor( g(x,y), 3 )   # error, since we are dealing with 2 variables
ERROR: AssertionError: 1 <= r <= get_numvars()
 in diffTaylor at /Users/benet/.julia/v0.4/TaylorSeries/src/TaylorN.jl:774
 in diffTaylor at /Users/benet/.julia/v0.4/TaylorSeries/src/TaylorN.jl:804

```

`evaluate` can also be used for `TaylorN` objects, using it on vectors of
numbers (`Real` or `Complex`); the length of the vector must coincide with the number
of independent variables.

```julia
julia> evaluate(exy, [.1,.02]) == e^0.12
true

```

Functions to compute the gradient, Jacobian and
Hessian have also been implemented. Using the
functions $f(x,y) = x^3 + 2x^2 y - 7 x + 2$ and $g(x,y) = y-x^4$ defined above,
we may use `gradient` or `âˆ‡` (`\nabla+TAB`); the results are of
type `Array{TaylorN{T},1}`. To compute the Jacobian or Hessian of a vector field
evaluated at a point, we use `jacobian` and `hessian`:

```julia
julia> f1 = f(x,y)
 2.0 - 7.0 x + 1.0 xÂ³ + 2.0 xÂ² y + ð’ª(â€–xâ€–Â¹Â¹)

julia> g1 = g(x,y)
 1.0 y - 1.0 xâ´ + ð’ª(â€–xâ€–Â¹Â¹)

julia> gradient( g1 )
2-element Array{TaylorSeries.TaylorN{Float64},1}:
  - 4.0 xÂ³ + ð’ª(â€–xâ€–Â¹Â¹)
       1.0 + ð’ª(â€–xâ€–Â¹Â¹)

julia> âˆ‡(f1)
2-element Array{TaylorSeries.TaylorN{Float64},1}:
  - 7.0 + 3.0 xÂ² + 4.0 x y + ð’ª(â€–xâ€–Â¹Â¹)
                    2.0 xÂ² + ð’ª(â€–xâ€–Â¹Â¹)

julia> fg = f1-g1-2*f1*g1
 2.0 - 7.0 x - 5.0 y + 14.0 x y + 1.0 xÂ³ + 2.0 xÂ² y + 5.0 xâ´ - 2.0 xÂ³ y - 4.0 xÂ² yÂ² - 14.0 xâµ + 2.0 xâ· + 4.0 xâ¶ y + ð’ª(â€–xâ€–Â¹Â¹)

julia> jacobian([f1,g1], [2,1])
2x2 Array{Float64,2}:
  13.0  8.0
 -32.0  1.0

julia> hessian(fg, [1.0,1.0])
2x2 Array{Float64,2}:
 -26.0  20.0
  20.0  -8.0

```


# Examples

## 1. Four-square identity

The first example shows that the four-square identity holds:
\\begin{eqnarray}
(a_1+a_2+a_3+a_4)\\cdot(b_1+b_2+b_3+b_4) & = &
     (a_1 b_1 - a_2 b_2 - a_3 b_3 -a_4 b_4)^2 + \\qquad \\nonumber \\\\
\\label{eq:Euler}
  & & (a_1 b_2 - a_2 b_1 - a_3 b_4 -a_4 b_3)^2 + \\\\
  & & (a_1 b_3 - a_2 b_4 - a_3 b_1 -a_4 b_2)^2 + \\nonumber \\\\
  & & (a_1 b_4 - a_2 b_3 - a_3 b_2 -a_4 b_1)^2, \\nonumber
\\end{eqnarray}
as proved by Euler. The code can we found in one of the tests of the package.

First, we reset the maximum degree of the polynomial to 4, since the RHS
of the equation
has *a priori* terms of fourth order, and the number of independent variables to
8.

    {meta}
    DocTestSetup = quote
        using TaylorSeries
        a1, a2, a3, a4, b1, b2, b3, b4 = set_variables("Î±â‚ Î±â‚‚ Î±â‚ƒ Î±â‚„ Î²â‚ Î²â‚‚ Î²â‚ƒ Î²â‚„", order=4)
    end

```julia
julia> # Define the variables Î±â‚, ..., Î±â‚„, Î²â‚, ..., Î²â‚„
       make_variable(name, index::Int) = string(name, TaylorSeries.subscriptify(index))
make_variable (generic function with 1 method)

julia> variable_names = [make_variable("Î±", i) for i in 1:4];

julia> append!(variable_names, [make_variable("Î²", i) for i in 1:4]);

julia> # Create the Taylor objects (order 4, numvars=8)
       a1, a2, a3, a4, b1, b2, b3, b4 = set_variables(variable_names, order=4);

julia> a1
 1.0 Î±â‚ + ð’ª(â€–xâ€–âµ)

julia> b1
 1.0 Î²â‚ + ð’ª(â€–xâ€–âµ)

```

Now we compute each term appearing in (\\ref{eq:Euler}), and compare them

```julia
julia> # left-hand side

julia> lhs1 = a1^2 + a2^2 + a3^2 + a4^2 ;

julia> lhs2 = b1^2 + b2^2 + b3^2 + b4^2 ;

julia> lhs = lhs1 * lhs2
 1.0 Î±â‚Â² Î²â‚Â² + 1.0 Î±â‚Â² Î²â‚‚Â² + 1.0 Î±â‚Â² Î²â‚ƒÂ² + 1.0 Î±â‚Â² Î²â‚„Â² + 1.0 Î±â‚‚Â² Î²â‚Â² + 1.0 Î±â‚‚Â² Î²â‚‚Â² + 1.0 Î±â‚‚Â² Î²â‚ƒÂ² + 1.0 Î±â‚‚Â² Î²â‚„Â² + 1.0 Î±â‚ƒÂ² Î²â‚Â² + 1.0 Î±â‚ƒÂ² Î²â‚‚Â² + 1.0 Î±â‚ƒÂ² Î²â‚ƒÂ² + 1.0 Î±â‚ƒÂ² Î²â‚„Â² + 1.0 Î±â‚„Â² Î²â‚Â² + 1.0 Î±â‚„Â² Î²â‚‚Â² + 1.0 Î±â‚„Â² Î²â‚ƒÂ² + 1.0 Î±â‚„Â² Î²â‚„Â² + ð’ª(â€–xâ€–âµ)

julia> # right-hand side

julia> rhs1 = (a1*b1 - a2*b2 - a3*b3 - a4*b4)^2 ;

julia> rhs2 = (a1*b2 + a2*b1 + a3*b4 - a4*b3)^2 ;

julia> rhs3 = (a1*b3 - a2*b4 + a3*b1 + a4*b2)^2 ;

julia> rhs4 = (a1*b4 + a2*b3 - a3*b2 + a4*b1)^2
 1.0 Î±â‚Â² Î²â‚„Â² + 2.0 Î±â‚ Î±â‚‚ Î²â‚ƒ Î²â‚„ - 2.0 Î±â‚ Î±â‚ƒ Î²â‚‚ Î²â‚„ + 2.0 Î±â‚ Î±â‚„ Î²â‚ Î²â‚„ + 1.0 Î±â‚‚Â² Î²â‚ƒÂ² - 2.0 Î±â‚‚ Î±â‚ƒ Î²â‚‚ Î²â‚ƒ + 2.0 Î±â‚‚ Î±â‚„ Î²â‚ Î²â‚ƒ + 1.0 Î±â‚ƒÂ² Î²â‚‚Â² - 2.0 Î±â‚ƒ Î±â‚„ Î²â‚ Î²â‚‚ + 1.0 Î±â‚„Â² Î²â‚Â² + ð’ª(â€–xâ€–âµ)

julia> rhs = rhs1 + rhs2 + rhs3 + rhs4
 1.0 Î±â‚Â² Î²â‚Â² + 1.0 Î±â‚Â² Î²â‚‚Â² + 1.0 Î±â‚Â² Î²â‚ƒÂ² + 1.0 Î±â‚Â² Î²â‚„Â² + 1.0 Î±â‚‚Â² Î²â‚Â² + 1.0 Î±â‚‚Â² Î²â‚‚Â² + 1.0 Î±â‚‚Â² Î²â‚ƒÂ² + 1.0 Î±â‚‚Â² Î²â‚„Â² + 1.0 Î±â‚ƒÂ² Î²â‚Â² + 1.0 Î±â‚ƒÂ² Î²â‚‚Â² + 1.0 Î±â‚ƒÂ² Î²â‚ƒÂ² + 1.0 Î±â‚ƒÂ² Î²â‚„Â² + 1.0 Î±â‚„Â² Î²â‚Â² + 1.0 Î±â‚„Â² Î²â‚‚Â² + 1.0 Î±â‚„Â² Î²â‚ƒÂ² + 1.0 Î±â‚„Â² Î²â‚„Â² + ð’ª(â€–xâ€–âµ)

julia> lhs == rhs
true

```

The identity is thus satisfied. $\\square$.



## 2. Fateman test

Richard J. Fateman, from Berkley, proposed as a stringent test
of polynomial multiplication
the evaluation of $s*(s+1)$, where $s = (1+x+y+z+w)^{20}$. This is
implemented in
the function `fateman1`. We shall also evaluate the form $s^2+s$ in `fateman2`,
which involves fewer operations (and makes a fairer comparison to what
Mathematica does). Below we have used Julia v0.4.

    {meta}
    DocTestSetup = quote
        using TaylorSeries
        set_variables("x", numvars=4, order=40);
    end

```julia
julia> set_variables("x", numvars=4, order=40);

julia> function fateman1(degree::Int)
           T = Int128
           oneH = HomogeneousPolynomial(one(T), 0)
           # s = 1 + x + y + z + w
           s = TaylorN( [oneH, HomogeneousPolynomial([one(T),one(T),one(T),one(T)],1)], degree )
           s = s^degree
           # s is converted to order 2*ndeg
           s = TaylorN(s, 2*degree)
           s * ( s+TaylorN(oneH, 2*degree) )
       end
fateman1 (generic function with 1 method)

julia> @time f1 = fateman1(0);
  0.257372 seconds (210.58 k allocations: 9.605 MB, 2.08% gc time)

julia> @time f1 = fateman1(20);
  2.510551 seconds (3.43 k allocations: 20.112 MB, 0.18% gc time)

```

Another implementation of the same:

```julia
julia> function fateman2(degree::Int)
           T = Int128
           oneH = HomogeneousPolynomial(one(T), 0)
           s = TaylorN( [oneH, HomogeneousPolynomial([one(T),one(T),one(T),one(T)],1)], degree )
           s = s^degree
           # s is converted to order 2*ndeg
           s = TaylorN(s, 2*degree)
           return s^2 + s
       end
fateman2 (generic function with 1 method)

julia> @time f2 = fateman2(0);
  0.009598 seconds (7.26 k allocations: 320.872 KB)

julia> @time f2 = fateman2(20);
  1.219438 seconds (3.42 k allocations: 20.111 MB, 0.56% gc time)

julia> get_coeff(f2,[1,6,7,20]) # coef x^1 y^6 z^7 w^{20}
128358585324486316800

julia> sum(TaylorSeries.size_table) # number of distinct monomials
135751

```

The tests above show the necessity of using `Int128`, that
`fateman2` is nearly twice as fast as `fateman1`, and that the series has 135751
monomials on 4 variables.

Mathematica (version 10.2) requires 3.139831 seconds. Then,
with TaylorSeries v0.1.2, our implementation of `fateman1` is about 20% faster,
and `fateman2` is more than a factor 2 faster. (The original test by Fateman
corresponds to `fateman1`, which avoids optimizations in `^2`.)
